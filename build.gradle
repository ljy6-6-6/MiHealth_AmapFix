buildscript {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
    }
}

plugins {
    id 'com.android.application' version '8.7.0' apply false
    id 'com.android.library'    version '8.7.0' apply false
    id 'org.jetbrains.kotlin.android' version '2.0.20' apply false
    id 'org.jetbrains.kotlin.jvm'     version '2.0.20' apply false
}

// 统一所有子模块的 Java/Kotlin 目标为 21（不使用 kotlin { jvmToolchain }，避免兼容性问题）
subprojects { sub ->
    // Java Toolchain（供 :checks 等 JVM 模块）
    sub.plugins.withId('java') {
        sub.extensions.configure(JavaPluginExtension) {
            toolchain.languageVersion = JavaLanguageVersion.of(21)
        }
    }
    // Android 模块（app / library）
    ['com.android.application', 'com.android.library'].each { pid ->
        sub.plugins.withId(pid) {
            def androidExt = sub.extensions.findByName('android')
            if (androidExt != null) {
                androidExt.compileOptions {
                    sourceCompatibility = JavaVersion.VERSION_21
                    targetCompatibility = JavaVersion.VERSION_21
                }
                // 旧写法兜底：统一 Kotlin jvmTarget=21（最稳）
                androidExt.kotlinOptions {
                    jvmTarget = '21'
                }
            }
        }
    }
    // 对所有 KotlinCompile 任务再兜一层（防第三方插件覆盖）
    sub.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = '21'
            freeCompilerArgs += ['-Xjvm-default=all']
        }
    }
}
