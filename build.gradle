buildscript {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
    }
}

plugins {
    id 'com.android.application' version '8.7.0' apply false
    id 'com.android.library'    version '8.7.0' apply false
    id 'org.jetbrains.kotlin.android' version '2.0.20' apply false
    id 'org.jetbrains.kotlin.jvm'     version '2.0.20' apply false
}

subprojects { sub ->
    sub.plugins.withId('java') {
        sub.extensions.configure(JavaPluginExtension) {
            toolchain.languageVersion = JavaLanguageVersion.of(21)
        }
    }
    // Android 模块（app / library）
    ['com.android.application', 'com.android.library'].each { pid ->
        sub.plugins.withId(pid) {
            def androidExt = sub.extensions.findByName('android')
            if (androidExt != null) {
                androidExt.compileOptions {
                    sourceCompatibility = JavaVersion.VERSION_21
                    targetCompatibility = JavaVersion.VERSION_21
                }
            }
            // Kotlin Android
            sub.plugins.withId('org.jetbrains.kotlin.android') {
                sub.extensions.configure(org.jetbrains.kotlin.gradle.dsl.KotlinAndroidProjectExtension) {
                    jvmToolchain(21)
                    compilerOptions {
                        jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_21)
                    }
                }
            }
        }
    }
    // Kotlin JVM 模块
    sub.plugins.withId('org.jetbrains.kotlin.jvm') {
        sub.extensions.configure(org.jetbrains.kotlin.gradle.dsl.KotlinJvmProjectExtension) {
            jvmToolchain(21)
            compilerOptions {
                jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_21)
            }
        }
    }
}
